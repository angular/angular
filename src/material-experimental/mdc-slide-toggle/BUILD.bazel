package(default_visibility = ["//visibility:public"])

load("@io_bazel_rules_sass//:defs.bzl", "sass_binary", "sass_library")
load("//src/e2e-app:test_suite.bzl", "e2e_test_suite")
load("//tools:defaults.bzl", "ng_e2e_test_library", "ng_module", "ng_test_library", "ng_web_test_suite", "ts_library")

ng_module(
    name = "mdc-slide-toggle",
    srcs = glob(
        ["**/*.ts"],
        exclude = [
            "**/*.spec.ts",
            "harness/**",
        ],
    ),
    assets = [":slide_toggle_scss"] + glob(["**/*.html"]),
    module_name = "@angular/material-experimental/mdc-slide-toggle",
    deps = [
        "//src/cdk/coercion",
        "//src/material/core",
        "//src/material/slide-toggle",
        "@npm//@angular/animations",
        "@npm//@angular/common",
        "@npm//@angular/core",
        "@npm//@angular/forms",
        "@npm//@material/ripple",
        "@npm//@material/switch",
    ],
)

ts_library(
    name = "harness",
    srcs = glob(
        ["harness/**/*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    deps = [
        "//src/cdk-experimental/testing",
        "//src/cdk/coercion",
    ],
)

sass_library(
    name = "mdc_slide_toggle_scss_lib",
    srcs = glob(["**/_*.scss"]),
    deps = [
        "//src/material-experimental/mdc-helpers:mdc_helpers_scss_lib",
        "//src/material-experimental/mdc-helpers:mdc_scss_deps_lib",
    ],
)

sass_binary(
    name = "slide_toggle_scss",
    src = "slide-toggle.scss",
    include_paths = [
        "external/npm/node_modules",
    ],
    deps = [
        "//src/material-experimental/mdc-helpers:mdc_helpers_scss_lib",
        "//src/material-experimental/mdc-helpers:mdc_scss_deps_lib",
    ],
)

ng_test_library(
    name = "slide_toggle_tests_lib",
    srcs = glob(
        ["**/*.spec.ts"],
        exclude = ["**/*.e2e.spec.ts"],
    ),
    deps = [
        ":harness",
        ":mdc-slide-toggle",
        "//src/cdk-experimental/testing",
        "//src/cdk-experimental/testing/testbed",
        "//src/cdk/bidi",
        "//src/cdk/testing",
        "//src/material/slide-toggle",
        "@npm//@angular/forms",
        "@npm//@angular/platform-browser",
    ],
)

ng_web_test_suite(
    name = "unit_tests",
    static_files = [
        "@npm//:node_modules/@material/ripple/dist/mdc.ripple.js",
        "@npm//:node_modules/@material/switch/dist/mdc.switch.js",
    ],
    deps = [
        ":slide_toggle_tests_lib",
        "//src/material-experimental:mdc_require_config.js",
    ],
)

ng_e2e_test_library(
    name = "e2e_test_sources",
    srcs = glob(["**/*.e2e.spec.ts"]),
    deps = [
        "//src/cdk/private/testing/e2e",
    ],
)

e2e_test_suite(
    name = "e2e_tests",
    deps = [
        ":e2e_test_sources",
        "//src/cdk/private/testing/e2e",
    ],
)
