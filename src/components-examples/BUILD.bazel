load("//tools:defaults.bzl", "ng_module", "ng_package")
load("//tools/highlight-files:index.bzl", "highlight_files")
load("//tools/package-docs-content:index.bzl", "package_docs_content")

package(default_visibility = ["//visibility:public"])

ALL_EXAMPLES = [
    # TODO(devversion): try to have for each entry-point a bazel package so that
    # we can automate this using the "package.bzl" variables. Currently generated
    # with "bazel query 'kind("ng_module", //src/components-examples/...:*)' --output="label"
    "//src/components-examples/material/tree",
    "//src/components-examples/material/tooltip",
    "//src/components-examples/material/toolbar",
    "//src/components-examples/material/tabs",
    "//src/components-examples/material/table",
    "//src/components-examples/material/stepper",
    "//src/components-examples/material/sort",
    "//src/components-examples/material/snack-bar",
    "//src/components-examples/material/slider",
    "//src/components-examples/material/slide-toggle",
    "//src/components-examples/material/sidenav",
    "//src/components-examples/material/select",
    "//src/components-examples/material/radio",
    "//src/components-examples/material/progress-spinner",
    "//src/components-examples/material/progress-bar",
    "//src/components-examples/material/paginator",
    "//src/components-examples/material/menu",
    "//src/components-examples/material/list",
    "//src/components-examples/material/input",
    "//src/components-examples/material/icon",
    "//src/components-examples/material/grid-list",
    "//src/components-examples/material/form-field",
    "//src/components-examples/material/expansion",
    "//src/components-examples/material/divider",
    "//src/components-examples/material/dialog",
    "//src/components-examples/material/datepicker",
    "//src/components-examples/material/core",
    "//src/components-examples/material/chips",
    "//src/components-examples/material/checkbox",
    "//src/components-examples/material/card",
    "//src/components-examples/material/button-toggle",
    "//src/components-examples/material/button",
    "//src/components-examples/material/bottom-sheet",
    "//src/components-examples/material/badge",
    "//src/components-examples/material/autocomplete",
    "//src/components-examples/material-experimental/column-resize",
    "//src/components-examples/material-experimental/popover-edit",
    "//src/components-examples/material-experimental/mdc-card",
    "//src/components-examples/material-experimental/mdc-form-field",
    "//src/components-examples/material-experimental/selection",
    "//src/components-examples/cdk/tree",
    "//src/components-examples/cdk/text-field",
    "//src/components-examples/cdk/table",
    "//src/components-examples/cdk/stepper",
    "//src/components-examples/cdk/scrolling",
    "//src/components-examples/cdk/portal",
    "//src/components-examples/cdk/accordion",
    "//src/components-examples/cdk/platform",
    "//src/components-examples/cdk/drag-drop",
    "//src/components-examples/cdk/clipboard",
    "//src/components-examples/cdk/a11y",
    "//src/components-examples/cdk/layout",
    "//src/components-examples/cdk/overlay",
    "//src/components-examples/cdk-experimental/menu",
    "//src/components-examples/cdk-experimental/popover-edit",
    "//src/components-examples/cdk-experimental/selection",
]

ng_module(
    name = "components-examples",
    srcs = glob(["*.ts"]) + [":example-module.ts"],
    deps = ALL_EXAMPLES,
)

filegroup(
    name = "example-source-files",
    srcs = ["%s:source-files" % pkg for pkg in ALL_EXAMPLES],
)

highlight_files(
    name = "examples-highlighted",
    srcs = [":example-source-files"],
)

package_docs_content(
    name = "docs-content",
    srcs = {
        # We want to package the guides in to the docs content. These will be displayed
        # in the documentation.
        "//guides": "guides",

        # Package the overviews for "@angular/material" and "@angular/cdk" into the docs content
        "//src/cdk:overviews": "overviews/cdk",
        "//src/material:overviews": "overviews/material",

        # Package the API docs for the Material and CDK package into the docs-content
        "//src:api-docs": "api-docs",

        # In order to be able to run examples in StackBlitz, we also want to package the
        # plain source files into the docs-content.
        ":example-source-files": "examples-source",

        # For the live-examples in our docs, we want to package the highlighted files
        # into the docs content. These will be used to show the source code for examples.
        # Note: `examples-highlighted` is a tree artifact that we want to store as is
        # in the docs-content. Hence there is no target section name.
        ":examples-highlighted": "",
    },
)

ng_package(
    name = "npm_package",
    srcs = ["package.json"],
    entry_point = ":public-api.ts",
    # this is a workaround to store a tree artifact in the ng_package.
    # ng_package does not properly handle tree artifacts currently so we escalate to nested_packages
    nested_packages = [":docs-content"],
    tags = ["docs-package"],
    deps = [":components-examples"] + ALL_EXAMPLES,
)

genrule(
    name = "example-module",
    srcs = [":example-source-files"],
    outs = [
        "example-module.ts",
        "_example_module.MF",
    ],
    cmd = """
      # Create source file manifest
      echo "$(execpaths //src/components-examples:example-source-files)" \
          > $(execpath _example_module.MF)

      # Run the bazel entry-point for generating the example module.
      ./$(execpath //tools/example-module:bazel-bin) \
          "$(execpath _example_module.MF)" \
          "$(execpath example-module.ts)" \
          "$$PWD/src/components-examples"
    """,
    output_to_bindir = True,
    tools = ["//tools/example-module:bazel-bin"],
)
