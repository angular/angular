package(default_visibility = ["//visibility:public"])

load("//:packages.bzl", "CDK_PACKAGES", "CDK_TARGETS", "CDK_TARGETS_WITH_STYLES", "ROLLUP_GLOBALS")
load("//tools:defaults.bzl", "ng_module", "ng_package")

# Root "@angular/cdk" entry-point that does not re-export individual entry-points.
ng_module(
    name = "cdk",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    module_name = "@angular/cdk",
    deps = [
        "@npm//@angular/core",
    ],
)

# List of style files that need to be copied to the root of the CDK package. We do this
# to make it easier for developers to import these styles without needing to know about
# deep imports in the release output. Note that this is done for backwards compatibility
# with the Gulp release output. TODO(devversion): consider removing this in the future.
rerootedStyles = [file for target in CDK_TARGETS_WITH_STYLES for file in [
    [
        "_%s.scss" % target,
        target,
    ],
    [
        "%s-prebuilt.css" % target,
        target,
    ],
    [
        "%s-prebuilt.css.map" % target,
        target,
    ],
]]

# Create genrules which re-root stylesheets from secondary entry-points.
[genrule(
    name = "%s_rerooted" % file,
    srcs = ["//src/cdk/%s:%s" % (target, file)],
    outs = [file],
    cmd = "cp $< $@",
) for [
    file,
    target,
] in rerootedStyles]

# List of targets which generate the re-rooted stylesheet files.
rerootedStyleTargets = ["%s_rerooted" % file for [
    file,
    _,
] in rerootedStyles]

# Creates the @angular/cdk package published to npm.
ng_package(
    name = "npm_package",
    srcs = ["package.json"],
    data = ["%s_rerooted" % file for [
        file,
        _,
    ] in rerootedStyles],
    entry_point = ":public-api.ts",
    globals = ROLLUP_GLOBALS,
    packages = [
        "//src/cdk/schematics:npm_package",
        "//src/cdk/testing:npm_package",
    ],
    tags = ["release-package"],
    deps = CDK_TARGETS,
)

filegroup(
    name = "overviews",
    srcs = ["//src/cdk/%s:overview" % name for name in CDK_PACKAGES],
)
