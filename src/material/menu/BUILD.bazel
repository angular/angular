package(default_visibility = ["//visibility:public"])

load("@io_bazel_rules_sass//:defs.bzl", "sass_binary", "sass_library")
load("@npm_angular_bazel//:index.bzl", "protractor_web_test_suite")
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_e2e_test_library",
    "ng_module",
    "ng_test_library",
    "ng_web_test_suite",
)

ng_module(
    name = "menu",
    srcs = glob(
        ["**/*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    assets = [":menu.css"] + glob(["**/*.html"]),
    module_name = "@angular/material/menu",
    deps = [
        "//src/cdk/a11y",
        "//src/cdk/bidi",
        "//src/cdk/coercion",
        "//src/cdk/keycodes",
        "//src/cdk/overlay",
        "//src/cdk/platform",
        "//src/cdk/portal",
        "//src/material/core",
        "@npm//@angular/animations",
        "@npm//@angular/common",
        "@npm//@angular/core",
        "@npm//rxjs",
    ],
)

sass_library(
    name = "menu_scss_lib",
    srcs = glob(["**/_*.scss"]),
    deps = ["//src/material/core:core_scss_lib"],
)

sass_binary(
    name = "menu_scss",
    src = "menu.scss",
    deps = [
        "//src/cdk/a11y:a11y_scss_lib",
        "//src/material/core:core_scss_lib",
    ],
)

ng_test_library(
    name = "unit_test_sources",
    srcs = glob(
        ["**/*.spec.ts"],
        exclude = ["**/*.e2e.spec.ts"],
    ),
    deps = [
        ":menu",
        "//src/cdk/a11y",
        "//src/cdk/bidi",
        "//src/cdk/keycodes",
        "//src/cdk/overlay",
        "//src/cdk/scrolling",
        "//src/cdk/testing",
        "//src/material/core",
        "@npm//@angular/platform-browser",
        "@npm//rxjs",
    ],
)

ng_web_test_suite(
    name = "unit_tests",
    deps = [":unit_test_sources"],
)

ng_e2e_test_library(
    name = "e2e_test_sources",
    srcs = glob(["**/*.e2e.spec.ts"]),
    deps = [
        "//src/cdk/testing/e2e",
    ],
)

protractor_web_test_suite(
    name = "e2e_tests",
    configuration = "//src/e2e-app:protractor.conf.js",
    data = [
        "//tools/axe-protractor",
        "@npm//@angular/bazel",
    ],
    on_prepare = "//src/e2e-app:start-devserver.js",
    server = "//src/e2e-app:devserver",
    tags = ["e2e"],
    deps = [
        ":e2e_test_sources",
        "//src/cdk/testing/e2e",
    ],
)

markdown_to_html(
    name = "overview",
    srcs = [":menu.md"],
)

filegroup(
    name = "source-files",
    srcs = glob(["**/*.ts"]),
)
