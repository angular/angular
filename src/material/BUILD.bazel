load(
    "//src/material:config.bzl",
    "MATERIAL_ENTRYPOINTS",
    "MATERIAL_SCSS_LIBS",
    "MATERIAL_TARGETS",
    "MATERIAL_TESTING_TARGETS",
)
load("//tools:defaults.bzl", "ng_package", "sass_library", "ts_library")

package(default_visibility = ["//visibility:public"])

ts_library(
    name = "material",
    srcs = ["index.ts"],
)

filegroup(
    name = "overviews",
    srcs = ["//src/material/%s:overview" % name for name in MATERIAL_ENTRYPOINTS],
)

sass_library(
    name = "sass_lib",
    srcs = [
        "_index.scss",
        "_theming.scss",
    ],
    deps = [
        "//src/material/core:core_scss_lib",
        "//src/material/core:theming_scss_lib",
        "//src/material/legacy-core:legacy_core_scss_lib",
    ],
)

# Generate the material `package.json` by adding all MDC dependencies
# from the project root to the `package-base.json` file. This is done to avoid
# duplication and make the update process of MDC dependencies more obvious.
# Note that we need to explicitly reference all individual `@material/` dependencies as
# our code imports from the individual dependencies (and not from the kitchen-sink package)
genrule(
    name = "package_json",
    srcs = ["package-base.json"],
    outs = ["package.json"],
    cmd = "$(execpath //tools/mdc-deps:add-to-package-json) $< > $@",
    exec_tools = ["//tools/mdc-deps:add-to-package-json"],
)

# Creates the @angular/material package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        ":package_json",
        ":sass_lib",
        "//src/material/core:theming_scss_lib",
        "//src/material/prebuilt-themes:deeppurple-amber",
        "//src/material/prebuilt-themes:indigo-pink",
        "//src/material/prebuilt-themes:pink-bluegrey",
        "//src/material/prebuilt-themes:purple-green",
        "//src/material/legacy-prebuilt-themes:deeppurple-amber",
        "//src/material/legacy-prebuilt-themes:indigo-pink",
        "//src/material/legacy-prebuilt-themes:pink-bluegrey",
        "//src/material/legacy-prebuilt-themes:purple-green",
    ] + MATERIAL_SCSS_LIBS,
    nested_packages = ["//src/material/schematics:npm_package"],
    tags = ["release-package"],
    deps = MATERIAL_TARGETS + MATERIAL_TESTING_TARGETS,
)
