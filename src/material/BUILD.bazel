package(default_visibility = ["//visibility:public"])

load("@npm//scss-bundle:index.bzl", "scss_bundle")
load("//src/cdk:config.bzl", "CDK_SCSS_LIBS")
load(
    "//src/material:config.bzl",
    "MATERIAL_ENTRYPOINTS",
    "MATERIAL_SCSS_LIBS",
    "MATERIAL_TARGETS",
    "MATERIAL_TESTING_TARGETS",
)
load("//tools:defaults.bzl", "ng_package", "ts_library")

ts_library(
    name = "material",
    srcs = ["index.ts"],
)

filegroup(
    name = "overviews",
    srcs = ["//src/material/%s:overview" % name for name in MATERIAL_ENTRYPOINTS],
)

scss_bundle(
    name = "theming_bundle",
    outs = ["_theming.scss"],
    args = [
        "--entry=$(location //src/material/core:theming/_all-theme.scss)",
        "--verbosity=Errors",
        "--dedupe=./src/**/*.scss",
        "--dest=$(location :_theming.scss)",
    ],
    data = CDK_SCSS_LIBS + MATERIAL_SCSS_LIBS + ["//src/material/core:theming/_all-theme.scss"],
)

# Creates the @angular/material package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    data = [
        ":theming_bundle",
        "//src/material/core:theming_scss_lib",
        "//src/material/prebuilt-themes:deeppurple-amber",
        "//src/material/prebuilt-themes:indigo-pink",
        "//src/material/prebuilt-themes:pink-bluegrey",
        "//src/material/prebuilt-themes:purple-green",
    ] + MATERIAL_SCSS_LIBS,
    entry_point = ":index.ts",
    entry_point_name = "material",
    packages = ["//src/material/schematics:npm_package"],
    tags = ["release-package"],
    deps = MATERIAL_TARGETS + MATERIAL_TESTING_TARGETS,
)
