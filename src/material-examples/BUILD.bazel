package(default_visibility = ["//visibility:public"])

load("//:packages.bzl", "ROLLUP_GLOBALS")
load("//tools:defaults.bzl", "ng_module", "ng_package")
load("//tools/highlight-files:index.bzl", "highlight_files")
load("//tools/package-docs-content:index.bzl", "package_docs_content")

EXAMPLE_PACKAGES = [
    # TODO(devversion): try to have for each entry-point a bazel package so that
    # we can automate this using the "package.bzl" variables.
    "//src/material-examples/cdk/a11y",
    "//src/material-examples/cdk/drag-drop",
    "//src/material-examples/cdk/platform",
    "//src/material-examples/cdk-experimental/popover-edit",
    "//src/material-examples/cdk/portal",
    "//src/material-examples/cdk/scrolling",
    "//src/material-examples/cdk/stepper",
    "//src/material-examples/cdk/table",
    "//src/material-examples/cdk/text-field",
    "//src/material-examples/cdk/tree",
    "//src/material-examples/material/autocomplete",
    "//src/material-examples/material/badge",
    "//src/material-examples/material/bottom-sheet",
    "//src/material-examples/material/button",
    "//src/material-examples/material/button-toggle",
    "//src/material-examples/material/card",
    "//src/material-examples/material/checkbox",
    "//src/material-examples/material/chips",
    "//src/material-examples/material/core",
    "//src/material-examples/material/datepicker",
    "//src/material-examples/material/dialog",
    "//src/material-examples/material/divider",
    "//src/material-examples/material/expansion",
    "//src/material-examples/material/form-field",
    "//src/material-examples/material/grid-list",
    "//src/material-examples/material/icon",
    "//src/material-examples/material/input",
    "//src/material-examples/material/list",
    "//src/material-examples/material/menu",
    "//src/material-examples/material/paginator",
    "//src/material-examples/material-experimental/popover-edit",
    "//src/material-examples/material/progress-bar",
    "//src/material-examples/material/progress-spinner",
    "//src/material-examples/material/radio",
    "//src/material-examples/material/select",
    "//src/material-examples/material/sidenav",
    "//src/material-examples/material/slide-toggle",
    "//src/material-examples/material/slider",
    "//src/material-examples/material/snack-bar",
    "//src/material-examples/material/sort",
    "//src/material-examples/material/stepper",
    "//src/material-examples/material/table",
    "//src/material-examples/material/tabs",
    "//src/material-examples/material/toolbar",
    "//src/material-examples/material/tooltip",
    "//src/material-examples/material/tree",
]

ng_module(
    name = "examples",
    # TODO(paul): re-add the example-module genfile once angular/angular#30259 is resolved.
    srcs = glob(["**/*.ts"]),
    module_name = "@angular/material-examples",
    deps = EXAMPLE_PACKAGES,
)

filegroup(
    name = "example-source-files",
    srcs = ["%s:source-files" % pkg for pkg in EXAMPLE_PACKAGES],
)

highlight_files(
    name = "highlighted-source-files",
    srcs = [":example-source-files"],
    tags = ["docs-package"],
)

package_docs_content(
    name = "docs-content",
    srcs = {
        # We want to package the guides in to the docs content. These will be displayed
        # in the documentation.
        "//guides": "guides",

        # Package the overviews for "@angular/material" and "@angular/cdk" into the docs content
        "//src/cdk:overviews": "overviews/cdk",
        "//src/material:overviews": "overviews/material",

        # Package the API docs for the Material and CDK package into the docs-content
        "//src:api-docs": "api-docs",

        # In order to be able to run examples in StackBlitz, we also want to package the
        # plain source files into the docs-content.
        ":example-source-files": "examples-source",

        # For the live-examples in our docs, we want to package the highlighted files
        # into the docs content. These will be used to show the source code for examples.
        ":highlighted-source-files": "examples-highlighted",
    },
    tags = ["docs-package"],
)

ng_package(
    name = "npm_package",
    srcs = ["package.json"],
    data = [":docs-content"],
    entry_point = ":public-api.ts",
    globals = ROLLUP_GLOBALS,
    tags = ["docs-package"],
    deps = [":examples"] + EXAMPLE_PACKAGES,
)

genrule(
    name = "example-module",
    # In case the example-module.ts file is present as an actual source file (e.g. generated
    # through Gulp), we need to exclude it because otherwise the genrule would fail.
    # TODO(devversion): remove this once gulp has been replaced with bazel.
    srcs = glob(
        ["**/*.ts"],
        exclude = ["example-module.ts"],
    ),
    # TODO(devversion): we can't name the genrule output "example-module.ts" as it would
    # get accidentally picked up when building the example module. See: angular/angular#30259
    outs = ["example-module_generated.ts"],
    cmd = """
    # As a workaround for https://github.com/bazelbuild/rules_nodejs/issues/404, we pass the
    # data to the Bazel entry-point through environment variables.
    export _SOURCE_FILES="$(SRCS)"
    export _OUTPUT_FILE="$@"
    export _BASE_DIR="$$(dirname $(location //src/material-examples:index.ts))"

    # Run the bazel entry-point for generating the example module.
    ./$(location //tools/example-module:bazel-bin)
  """,
    output_to_bindir = True,
    tools = ["//tools/example-module:bazel-bin"],
)
