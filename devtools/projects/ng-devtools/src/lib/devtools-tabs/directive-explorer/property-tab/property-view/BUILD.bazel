load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("//devtools/tools:ng_module.bzl", "ng_module")

package(default_visibility = ["//visibility:public"])

_STYLE_SRCS = [
    "property-editor.component.scss",
    "property-preview.component.scss",
    "property-tab-body.component.scss",
    "property-view.component.scss",
    "property-view-body.component.scss",
    "property-view-header.component.scss",
    "property-view-tree.component.scss",
]

_STYLE_LABELS = [
    src[:-len(".component.scss")].replace("-", "_") + "_styles"
    for src in _STYLE_SRCS
]

[
    sass_binary(
        name = label,
        src = src,
    )
    for label, src in zip(_STYLE_LABELS, _STYLE_SRCS)
]

ng_module(
    name = "property-view",
    srcs = [
        "property-editor.component.ts",
        "property-preview.component.ts",
        "property-tab-body.component.ts",
        "property-view.component.ts",
        "property-view.module.ts",
        "property-view-body.component.ts",
        "property-view-header.component.ts",
        "property-view-tree.component.ts",
    ],
    angular_assets = [
        "property-view.component.html",
        "property-view-tree.component.html",
        "property-view-header.component.html",
        "property-view-body.component.html",
        "property-preview.component.html",
        "property-editor.component.html",
        "property-tab-body.component.html",
    ] + _STYLE_LABELS,
    deps = [
        "//devtools/projects/ng-devtools/src/lib/devtools-tabs/directive-explorer/directive-forest/index-forest",
        "//devtools/projects/ng-devtools/src/lib/devtools-tabs/directive-explorer/property-resolver",
        "//devtools/projects/protocol",
        "//packages/common",
        "//packages/core",
        "//packages/forms",
        "@npm//@angular/cdk",
        "@npm//@angular/material",
        "@npm//@types",
        "@npm//rxjs",
    ],
)
