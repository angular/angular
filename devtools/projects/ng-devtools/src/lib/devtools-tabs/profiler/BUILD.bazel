load("//devtools/tools:ng_project.bzl", "ng_project")
load("//tools:defaults2.bzl", "sass_binary")

package(default_visibility = ["//visibility:public"])

_STYLE_SRCS = [
    "profiler.component.scss",
    "profiler-import-dialog.component.scss",
]

_STYLE_LABELS = [
    src[:-len(".component.scss")].replace("-", "_") + "_styles"
    for src in _STYLE_SRCS
]

[
    sass_binary(
        name = label,
        src = src,
        deps = [
            "//devtools/projects/ng-devtools/src/styles:typography",
        ],
    )
    for label, src in zip(_STYLE_LABELS, _STYLE_SRCS)
]

ng_project(
    name = "profiler",
    srcs = [
        "file-api-service.ts",
        "profiler.component.ts",
        "profiler-import-dialog.component.ts",
    ],
    angular_assets = [
        "profiler.component.html",
        "profiler-import-dialog.component.html",
    ] + _STYLE_LABELS,
    interop_deps = [
        "//packages/common",
        "//packages/core",
        "//packages/forms",
    ],
    deps = [
        "//:node_modules/@angular/material",
        "//:node_modules/rxjs",
        "//devtools/projects/ng-devtools/src/lib/devtools-tabs/profiler/timeline:timeline_rjs",
        "//devtools/projects/protocol:protocol_rjs",
    ],
)
