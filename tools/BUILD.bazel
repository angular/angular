load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")

package(default_visibility = ["//visibility:public"])

exports_files([
    "system-config-tmpl.js",
    "system-rxjs-operators.js",
])

config_setting(
    name = "view_engine_mode",
    values = {
        "define": "angular_ivy_enabled=False",
    },
)

# Bazel build setting for enabling partial compilation. The partial compilation
# build setting can be set using `--//tools:partial_compilation=True`.
bool_flag(
    name = "partial_compilation",
    build_setting_default = False,
)

# Bazel config setting that matches if the partial compilation flag is set to `True`.
config_setting(
    name = "partial_compilation_enabled",
    flag_values = {
        ":partial_compilation": "True",
    },
)

# This file generates the `angular_ivy_enabled.js` file, which exports a truthy value
# whether Ivy should be enabled based on the `--defined=angular_ivy_enabled` value
# so runtime can detect which mode it is running in.
genrule(
    name = "angular_ivy_enabled",
    outs = ["angular_ivy_enabled.js"],
    cmd = "echo exports.ivyEnabled = \"'True' === '$(angular_ivy_enabled)'\"\\; > $@",
)
