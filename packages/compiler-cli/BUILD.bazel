load("@npm//@bazel/esbuild:index.bzl", "esbuild", "esbuild_config")
load("@npm2//:defs.bzl", "npm_link_all_packages")
load("//packages/bazel/src:ng_perf.bzl", "ng_perf_flag")
load("//tools:defaults.bzl", "api_golden_test", "extract_types", "pkg_npm", "ts_library")
load("//tools:defaults2.bzl", "npm_package", "ts_config")

package(default_visibility = ["//visibility:public"])

PUBLIC_TARGETS = [
    ":compiler-cli",
    "//packages/compiler-cli/private",
    "//packages/compiler-cli/linker",
    "//packages/compiler-cli/linker/babel",
]

npm_link_all_packages()

ts_config(
    name = "tsconfig_build",
    src = "tsconfig.json",
    deps = [
        "//:node_modules/@types/node",
        "//packages:tsconfig_build",
    ],
)

ts_config(
    name = "tsconfig_test",
    testonly = True,
    src = "tsconfig-test.json",
    deps = [
        ":tsconfig_build",
        "//:node_modules/@types/jasmine",
    ],
)

esbuild_config(
    name = "esbuild_config",
    config_file = "esbuild.config.js",
)

esbuild(
    name = "bundles",
    config = ":esbuild_config",
    entry_points = [
        ":index.ts",
        "//packages/compiler-cli:src/bin/ngc.ts",
        "//packages/compiler-cli:src/bin/ng_xi18n.ts",
        "//packages/compiler-cli/linker:index.ts",
        "//packages/compiler-cli/linker/babel:index.ts",
        "//packages/compiler-cli/private:bazel.ts",
        "//packages/compiler-cli/private:localize.ts",
        "//packages/compiler-cli/private:migrations.ts",
        "//packages/compiler-cli/private:tooling.ts",
    ],
    external = [
        "@angular/compiler",
        "typescript",
        "@babel/core",
        "reflect-metadata",
        "chokidar",
        "convert-source-map",
        "semver",
        "@jridgewell/sourcemap-codec",
        "tslib",
        "yargs",
    ],
    format = "esm",
    metafile = False,
    platform = "node",
    splitting = True,
    target = "node20",
    deps = PUBLIC_TARGETS,
)

ts_library(
    name = "compiler-cli",
    srcs = glob(
        [
            "*.ts",
            "src/**/*.ts",
        ],
        exclude = [
            "src/integrationtest/**/*.ts",
        ],
    ),
    deps = [
        "//packages/compiler",
        "//packages/compiler-cli/private",
        "//packages/compiler-cli/src/ngtsc/core",
        "//packages/compiler-cli/src/ngtsc/core:api",
        "//packages/compiler-cli/src/ngtsc/diagnostics",
        "//packages/compiler-cli/src/ngtsc/docs",
        "//packages/compiler-cli/src/ngtsc/file_system",
        "//packages/compiler-cli/src/ngtsc/incremental",
        "//packages/compiler-cli/src/ngtsc/indexer",
        "//packages/compiler-cli/src/ngtsc/logging",
        "//packages/compiler-cli/src/ngtsc/perf",
        "//packages/compiler-cli/src/ngtsc/program_driver",
        "//packages/compiler-cli/src/ngtsc/reflection",
        "//packages/compiler-cli/src/ngtsc/shims",
        "//packages/compiler-cli/src/ngtsc/transform/jit",
        "//packages/compiler-cli/src/ngtsc/translator",
        "//packages/compiler-cli/src/ngtsc/typecheck",
        "//packages/compiler-cli/src/ngtsc/typecheck/api",
        "//packages/compiler-cli/src/ngtsc/util",
        "@npm//@bazel/concatjs",
        "@npm//@types/node",
        "@npm//@types/yargs",
        "@npm//chokidar",
        "@npm//reflect-metadata",
        "@npm//typescript",
    ],
)

extract_types(
    name = "api_type_definitions",
    deps = PUBLIC_TARGETS,
)

pkg_npm(
    name = "npm_package",
    package_name = "@angular/compiler-cli",
    srcs = [
        "package.json",
    ],
    tags = [
        "release-with-framework",
    ],
    visibility = [
        "//adev:__pkg__",
        "//adev/shared-docs/pipeline/api-gen:__subpackages__",
        "//integration:__subpackages__",
        "//modules/benchmarks:__subpackages__",
        "//modules/playground:__subpackages__",
        "//modules/ssr-benchmarks:__subpackages__",
        "//packages/compiler-cli/integrationtest:__pkg__",
        "//packages/core/test/bundling:__subpackages__",
        "//packages/core/test/playground/zone-signal-input:__pkg__",
        "@npm//@angular/build-tooling/shared-scripts:__subpackages__",
    ],
    deps = [
        ":api_type_definitions",
        ":bundles",
    ],
)

# TODO(devversion): Temporary early `npm_package` for making compiler-cli usable in `rules_angular`.
# Long-term, `pkg_npm` will be migrated to `npm_package` in general.
npm_package(
    name = "pkg",
    srcs = [":npm_package"],
    data = [
        # Needed because compiler is a dev dependency (to satisfy the peer dependency)
        # and `rules_js` only makes transitive production dependencies available.
        ":node_modules/@angular/compiler",
    ],
    replace_prefixes = {
        "npm_package/": "",
    },
)

api_golden_test(
    name = "error_code_api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    entry_point = "angular/packages/compiler-cli/npm_package/src/ngtsc/diagnostics/src/error_code.d.ts",
    golden = "angular/goldens/public-api/compiler-cli/error_code.api.md",
)

api_golden_test(
    name = "extended_template_diagnostic_name_api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    entry_point = "angular/packages/compiler-cli/npm_package/src/ngtsc/diagnostics/src/extended_template_diagnostic_name.d.ts",
    golden = "angular/goldens/public-api/compiler-cli/extended_template_diagnostic_name.api.md",
)

api_golden_test(
    name = "compiler_options_api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    entry_point = "angular/packages/compiler-cli/npm_package/src/ngtsc/core/api/src/public_options.d.ts",
    golden = "angular/goldens/public-api/compiler-cli/compiler_options.api.md",
)

# Controls whether the Ivy compiler produces performance traces as part of each build
ng_perf_flag(
    name = "ng_perf",
    build_setting_default = False,
)
