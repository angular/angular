load("//tools:defaults2.bzl", "js_binary", "ts_project")

package(default_visibility = ["//visibility:public"])

BIN_ENTRYPOINTS = [
    "get-base-currencies-file",
    "get-base-locale-file",
    "get-closure-locale-file",
    "write-locale-files-to-dist",
]

ts_project(
    name = "bin",
    srcs = glob(["*.ts"]),
    # Disabled to remove doubling of runtime files as we are approaching the 70k limit when
    # CLDR files are also included in the binary action.
    deps = [
        "//packages/common/locales/generate-locales-tool",
    ],
)

[js_binary(
    name = entrypoint,
    data = [
        ":bin",
        "//packages:package_json",
        "@cldr_json_data//:ROOT_BUILD_FILE",
        "@cldr_json_data//:all_json",
        "@cldr_xml_data//:ROOT_BUILD_FILE",
        "@cldr_xml_data//:all_xml",
    ],
    entry_point = ":%s.js" % entrypoint,
    env = {
        "CLDR_JSON_DATA_RUNFILES_PATH": "$(rlocationpath @cldr_json_data//:ROOT_BUILD_FILE)",
        "CLDR_XML_DATA_RUNFILES_PATH": "$(rlocationpath @cldr_xml_data//:ROOT_BUILD_FILE)",
    },
    no_copy_to_bin = [
        "@cldr_xml_data//:all_xml",
        "@cldr_xml_data//:ROOT_BUILD_FILE",
        "@cldr_json_data//:all_json",
        "@cldr_json_data//:ROOT_BUILD_FILE",
    ],
) for entrypoint in BIN_ENTRYPOINTS]
