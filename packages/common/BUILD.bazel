load("@build_bazel_rules_nodejs//:index.bzl", "generated_file_test")
load("//packages/common/locales:index.bzl", "generate_base_currencies_file")
load("//tools:defaults.bzl", "api_golden_test", "api_golden_test_npm_package", "generate_api_docs", "ng_package")
load("//tools:defaults2.bzl", "ng_project", "npm_package")

package(default_visibility = ["//visibility:public"])

# This generates the `src/i18n/currencies.ts` file through the `generate-locales` tool. Since
# the base currencies file is checked-in for Google3, we add a `generated_file_test` to ensure
# the checked-in file is up-to-date. To disambiguate from the test, we use a more precise target
# name here.
generate_base_currencies_file(
    name = "base_currencies_file_generated",
    output_file = "base_currencies_generated.ts",
)

generated_file_test(
    name = "base_currencies_file",
    src = "src/i18n/currencies.ts",
    generated = ":base_currencies_file_generated",
)

ng_project(
    name = "common",
    srcs = glob(
        [
            "*.ts",
            "src/**/*.ts",
        ],
    ),
    interop_deps = [
        "//packages/core",
        "//packages/core/primitives/dom-navigation",
    ],
    deps = [
        "//:node_modules/rxjs",
    ],
)

ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    nested_packages = ["//packages/common/locales:package"],
    tags = [
        "release-with-framework",
    ],
    # Do not add more to this list.
    # Dependencies on the full npm_package cause long re-builds.
    visibility = [
        "//adev:__pkg__",
        "//adev/shared-docs:__subpackages__",
        "//integration:__subpackages__",
        "//modules/ssr-benchmarks:__subpackages__",
        "//packages/bazel/test/ng_package:__pkg__",
        "//packages/compiler-cli/integrationtest:__pkg__",
        "//packages/compiler-cli/test:__pkg__",
        "//packages/compiler-cli/test/diagnostics:__pkg__",
        "//packages/compiler-cli/test/transformers:__pkg__",
        "//packages/compiler/test:__pkg__",
        "//packages/language-service/test:__pkg__",
    ],
    deps = [
        "//packages/common",
        "//packages/common/http",
        "//packages/common/http/testing",
        "//packages/common/testing",
        "//packages/common/upgrade",
    ],
)

# TODO(devversion): Temporary early `npm_package` for making this package usable in `core/test/bundling`.
# Long-term, `pkg_npm` will be migrated to `npm_package` in general.
npm_package(
    name = "pkg",
    srcs = [":npm_package"],
    replace_prefixes = {
        "npm_package/": "",
    },
)

api_golden_test_npm_package(
    name = "common_api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    golden_dir = "angular/goldens/public-api/common",
    npm_package = "angular/packages/common/npm_package",
)

api_golden_test(
    name = "common_errors",
    data = [
        "//goldens:public-api",
        "//packages/common",
    ],
    entry_point = "angular/packages/common/src/errors.d.ts",
    golden = "angular/goldens/public-api/common/errors.api.md",
)

filegroup(
    name = "files_for_docgen",
    srcs = glob([
        "*.ts",
        "src/**/*.ts",
    ]) + ["PACKAGE.md"],
)

generate_api_docs(
    name = "common_docs",
    srcs = [
        ":files_for_docgen",
        "//packages:common_files_and_deps_for_docs",
        "//packages/platform-browser:files_for_docgen",
    ],
    entry_point = ":index.ts",
    module_name = "@angular/common",
)
