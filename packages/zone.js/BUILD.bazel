load("//packages/zone.js:bundles.bzl", "BUNDLES_ENTRY_POINTS")
load("//packages/zone.js:tools.bzl", "generate_rollup_bundle")
load("//tools:defaults.bzl", "pkg_npm")
load("//tools:defaults2.bzl", "extract_types", "npm_package", "ts_config", "ts_project")

package(default_visibility = ["//visibility:public"])

ts_config(
    name = "tsconfig_build",
    src = "tsconfig.json",
)

ts_config(
    name = "tsconfig_test",
    src = "tsconfig-test.json",
    deps = [
        ":tsconfig_build",
        "//:node_modules/@types/jasmine",
        "//:node_modules/@types/node",
    ],
)

exports_files([
    "tsconfig.json",
])

genrule(
    name = "LICENSE_copy",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $< $@",
)

genrule(
    name = "LICENSE_wrapped",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE.wrapped"],
    cmd = "(echo '/**\n @license' && cat $< && echo '*/') > $@",
)

ts_project(
    name = "zone_js",
    srcs = ["zone.ts"],
    visibility = ["//visibility:private"],
    deps = ["//packages/zone.js/lib:zone_d_ts"],
)

extract_types(
    name = "zone_d_ts",
    visibility = ["//visibility:private"],
    deps = [":zone_js"],
)

generate_rollup_bundle(
    bundles = BUNDLES_ENTRY_POINTS,
)

pkg_npm(
    name = "npm_package",
    package_name = "zone.js",
    srcs = [
        "CHANGELOG.md",
        "README.md",
        "package.json",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":LICENSE.wrapped",
        ":LICENSE_copy",
    ] + [
        "//packages/zone.js/bundles:" + b + "-es5.dist"
        for b in BUNDLES_ENTRY_POINTS.keys()
    ] + [
        "//packages/zone.js/bundles:" + b + "-es5.min.dist"
        for b in BUNDLES_ENTRY_POINTS.keys()
    ] + [
        "//packages/zone.js/fesm2015:" + b + "-es2015.dist"
        for b in BUNDLES_ENTRY_POINTS.keys()
    ] + [
        "//packages/zone.js/fesm2015:" + b + "-es2015.min.dist"
        for b in BUNDLES_ENTRY_POINTS.keys()
    ] + [":zone_d_ts"],
)

# TODO(devversion): Temporary early `npm_package` for making this package usable in `modules/`.
# Long-term, `pkg_npm` will be migrated to `npm_package` in general.
npm_package(
    name = "pkg",
    srcs = [":npm_package"],
    replace_prefixes = {
        "npm_package/": "",
    },
)
