load("//tools:defaults.bzl", "karma_web_test_suite", "nodejs_binary", "npm_package_bin")
load("//tools:defaults2.bzl", "angular_jasmine_test", "ng_project", "ts_project")

package(default_visibility = ["//visibility:private"])

TEST_FILES = glob(
    ["*.ts"],
    exclude = ["authoring_test_compiler.ts"],
)

TEST_INTEROP_DEPS = [
    "//packages/core",
    "//packages/core/rxjs-interop",
    "//packages/core/testing",
    "//packages/platform-browser",
    "//packages/core/primitives/signals",
]

TEST_DEPS = [
    "//:node_modules/rxjs",
]

ts_project(
    name = "test_compiler_lib",
    testonly = True,
    srcs = ["authoring_test_compiler.ts"],
    interop_deps = [
        "//packages/compiler-cli",
    ],
    deps = [
        "//:node_modules/typescript",
    ],
)

nodejs_binary(
    name = "test_compiler",
    testonly = True,
    data = [
        ":test_compiler_lib",
        "//packages:package_json",
    ],
    entry_point = ":authoring_test_compiler.js",
)

npm_package_bin(
    name = "processed_test_sources",
    testonly = True,
    outs = ["transformed_%s" % file for file in TEST_FILES],
    args = ["$(@D)"] + ["$(execpath %s)" % file for file in TEST_FILES],
    data = TEST_FILES,
    tool = ":test_compiler",
)

ts_project(
    name = "test_jit_lib",
    testonly = True,
    srcs = ["transformed_%s" % file for file in TEST_FILES],
    interop_deps = TEST_INTEROP_DEPS,
    deps = TEST_DEPS,
)

ng_project(
    name = "test_lib",
    testonly = True,
    srcs = TEST_FILES,
    interop_deps = TEST_INTEROP_DEPS,
    deps = TEST_DEPS,
)

angular_jasmine_test(
    name = "test",
    data = [
        ":test_lib_rjs",
    ],
)

angular_jasmine_test(
    name = "test_jit",
    data = [
        ":test_jit_lib_rjs",
    ],
)

karma_web_test_suite(
    name = "test_web",
    deps = [
        ":test_lib",
    ],
)

karma_web_test_suite(
    name = "test_jit_web",
    deps = [
        ":test_jit_lib",
    ],
)
