load("//tools:defaults.bzl", "jasmine_node_test", "karma_web_test_suite", "ng_module", "nodejs_binary", "npm_package_bin", "ts_library")

package(default_visibility = ["//visibility:private"])

TEST_FILES = glob(
    ["*.ts"],
    exclude = ["authoring_test_compiler.ts"],
)

TEST_DEPS = [
    "//packages/core",
    "//packages/core/rxjs-interop",
    "//packages/core/testing",
    "//packages/platform-browser",
    "//packages/core/primitives/signals",
    "@npm//rxjs",
]

ts_library(
    name = "test_compiler_lib",
    testonly = True,
    srcs = ["authoring_test_compiler.ts"],
    deps = [
        "//packages/compiler-cli",
        "//packages/compiler-cli/src/ngtsc/imports",
        "//packages/compiler-cli/src/ngtsc/partial_evaluator",
        "//packages/compiler-cli/src/ngtsc/reflection",
        "//packages/compiler-cli/src/ngtsc/transform/jit",
        "@npm//typescript",
    ],
)

nodejs_binary(
    name = "test_compiler",
    testonly = True,
    data = [":test_compiler_lib"],
    entry_point = ":authoring_test_compiler.ts",
)

npm_package_bin(
    name = "processed_test_sources",
    testonly = True,
    outs = ["transformed_%s" % file for file in TEST_FILES],
    args = ["$(@D)"] + ["$(execpath %s)" % file for file in TEST_FILES],
    data = TEST_FILES,
    tool = ":test_compiler",
)

ts_library(
    name = "test_jit_lib",
    testonly = True,
    srcs = ["transformed_%s" % file for file in TEST_FILES],
    deps = TEST_DEPS,
)

ng_module(
    name = "test_lib",
    testonly = True,
    srcs = TEST_FILES,
    deps = TEST_DEPS,
)

jasmine_node_test(
    name = "test",
    bootstrap = ["//tools/testing:node"],
    deps = [
        ":test_lib",
    ],
)

jasmine_node_test(
    name = "test_jit",
    bootstrap = ["//tools/testing:node"],
    deps = [
        ":test_jit_lib",
    ],
)

karma_web_test_suite(
    name = "test_web",
    deps = [
        ":test_lib",
    ],
)

karma_web_test_suite(
    name = "test_jit_web",
    deps = [
        ":test_jit_lib",
    ],
)
