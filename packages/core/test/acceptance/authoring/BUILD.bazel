load("//tools:defaults2.bzl", "angular_jasmine_test", "js_binary", "js_run_binary", "ng_project", "ng_web_test_suite", "ts_project")

package(default_visibility = ["//visibility:private"])

TEST_FILES = glob(
    ["*.ts"],
    exclude = ["authoring_test_compiler.ts"],
)

TEST_DEPS = [
    "//packages/core:core_rjs",
    "//packages/core/rxjs-interop:rxjs-interop_rjs",
    "//packages/core/testing:testing_rjs",
    "//packages/platform-browser:platform-browser_rjs",
    "//packages/core/primitives/signals:signals_rjs",
    "//:node_modules/rxjs",
]

ts_project(
    name = "test_compiler_lib",
    testonly = True,
    srcs = ["authoring_test_compiler.ts"],
    deps = [
        "//:node_modules/typescript",
        "//packages/compiler-cli:compiler-cli_rjs",
        "//packages/compiler-cli/src/ngtsc/imports:imports_rjs",
        "//packages/compiler-cli/src/ngtsc/partial_evaluator:partial_evaluator_rjs",
        "//packages/compiler-cli/src/ngtsc/reflection:reflection_rjs",
        "//packages/compiler-cli/src/ngtsc/transform/jit:jit_rjs",
    ],
)

js_binary(
    name = "test_compiler",
    testonly = True,
    data = [
        ":test_compiler_lib_rjs",
        "//packages:package_json",
    ],
    entry_point = ":authoring_test_compiler.js",
)

js_run_binary(
    name = "processed_test_sources",
    testonly = True,
    srcs = TEST_FILES,
    outs = ["transformed_%s" % file for file in TEST_FILES],
    args = ["../../../$(@D)"] + ["$(rootpath %s)" % file for file in TEST_FILES],
    tool = ":test_compiler",
)

ts_project(
    name = "test_jit_lib",
    testonly = True,
    srcs = ["transformed_%s" % file for file in TEST_FILES],
    deps = TEST_DEPS,
)

ng_project(
    name = "test_lib",
    testonly = True,
    srcs = TEST_FILES,
    deps = TEST_DEPS,
)

angular_jasmine_test(
    name = "test",
    data = [
        ":test_lib_rjs",
    ],
)

angular_jasmine_test(
    name = "test_jit",
    data = [
        ":test_jit_lib_rjs",
    ],
)

ng_web_test_suite(
    name = "test_web",
    deps = [
        ":test_lib_rjs",
    ],
)

ng_web_test_suite(
    name = "test_jit_web",
    deps = [
        ":test_jit_lib_rjs",
    ],
)
