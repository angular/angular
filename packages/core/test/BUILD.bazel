load("//tools:defaults.bzl", "jasmine_node_test", "karma_web_test_suite", "ts_library")
load("//tools/circular_dependency_test:index.bzl", "circular_dependency_test")
load("@npm//@babel/cli:index.bzl", "babel")
load("@npm//typescript:index.bzl", "tsc")

package(default_visibility = ["//visibility:public"])

circular_dependency_test(
    name = "circular_deps_test",
    entry_point = "angular/packages/core/index.js",
    deps = ["//packages/core"],
)

circular_dependency_test(
    name = "testing_circular_deps_test",
    entry_point = "angular/packages/core/testing/index.js",
    deps = ["//packages/core/testing"],
)

genrule(
    name = "downleveled_es5_fixture",
    srcs = ["reflection/es2015_inheritance_fixture.ts"],
    outs = ["reflection/es5_downleveled_inheritance_fixture.js"],
    cmd = """
        es2015_out_dir="/tmp/downleveled_es5_fixture/"
        es2015_out_file="$$es2015_out_dir/es2015_inheritance_fixture.js"

        # Build the ES2015 output and then downlevel it to ES5.
        $(execpath @npm//typescript/bin:tsc) $< --outDir $$es2015_out_dir --target ES2015 \
            --types --module umd
        $(execpath @npm//typescript/bin:tsc) --outFile $@ $$es2015_out_file --allowJs \
            --types --target ES5 --downlevelIteration
    """,
    tools = ["@npm//typescript/bin:tsc"],
)

tsc(
    name = "istanbul_inheritance_fixture_es2015",
    testonly = True,
    outs = ["reflection/istanbul_inheritance_fixture_es2015.js"],
    args = [
        "--target es2015",
        "--module umd",
        "--outDir $(@D)",
        "--skipLibCheck",
        "$(execpath reflection/istanbul_inheritance_fixture_es2015.ts)",
    ],
    data = ["reflection/istanbul_inheritance_fixture_es2015.ts"],
)

babel(
    name = "istanbul_inheritance_fixture_es2015_instrumented",
    testonly = True,
    outs = ["reflection/istanbul_inheritance_fixture_es2015.instrumented.js"],
    args = [
        "$(execpath :istanbul_inheritance_fixture_es2015)",
        "--plugins istanbul",
        "--out-file $@",
    ],
    data = [
        ":istanbul_inheritance_fixture_es2015",
        "@npm//babel-plugin-istanbul",
    ],
)

tsc(
    name = "istanbul_inheritance_fixture_es5",
    testonly = True,
    outs = ["reflection/istanbul_inheritance_fixture_es5.js"],
    args = [
        "--target es5",
        "--module umd",
        "--outDir $(@D)",
        "--skipLibCheck",
        "$(execpath reflection/istanbul_inheritance_fixture_es5.ts)",
    ],
    data = ["reflection/istanbul_inheritance_fixture_es5.ts"],
)

babel(
    name = "istanbul_inheritance_fixture_es5_instrumented",
    testonly = True,
    outs = ["reflection/istanbul_inheritance_fixture_es5.instrumented.js"],
    args = [
        "$(execpath :istanbul_inheritance_fixture_es5)",
        "--plugins istanbul",
        "--out-file $@",
    ],
    data = [
        ":istanbul_inheritance_fixture_es5",
        "@npm//babel-plugin-istanbul",
    ],
)

UTILS = [
    "linker/source_map_util.ts",
]

ts_library(
    name = "test_utils",
    testonly = True,
    srcs = UTILS,
    deps = [
        "//packages/compiler",
        "@npm//base64-js",
        "@npm//source-map",
    ],
)

ts_library(
    name = "test_lib",
    testonly = True,
    srcs = glob(
        ["**/*.ts"],
        exclude = UTILS + [
            "**/*_node_only_spec.ts",
            "reflection/es2015_inheritance_fixture.ts",
            "reflection/istanbul_inheritance_fixture_es2015.ts",
            "reflection/istanbul_inheritance_fixture_es5.ts",
        ],
    ),
    # Visible to //:saucelabs_unit_tests_poc target
    visibility = ["//:__pkg__"],
    deps = [
        ":test_utils",
        "//packages/animations",
        "//packages/animations/browser",
        "//packages/animations/browser/testing",
        "//packages/common",
        "//packages/common/locales",
        "//packages/compiler",
        "//packages/core",
        "//packages/core/src/di/interface",
        "//packages/core/src/interface",
        "//packages/core/src/reflection",
        "//packages/core/src/util",
        "//packages/core/testing",
        "//packages/localize/init",
        "//packages/platform-browser",
        "//packages/platform-browser-dynamic",
        "//packages/platform-browser/animations",
        "//packages/platform-browser/testing",
        "//packages/private/testing",
        "//packages/router",
        "//packages/router/testing",
        "//packages/zone.js/lib:zone_d_ts",
        "@npm//rxjs",
    ],
)

ts_library(
    name = "test_node_only_lib",
    testonly = True,
    srcs = glob(
        ["**/*_node_only_spec.ts"],
        exclude = UTILS,
    ),
    deps = [
        ":test_lib",
        ":test_utils",
        "//packages/compiler",
        "//packages/core",
        "//packages/core/src/compiler",
        "//packages/core/testing",
        "//packages/platform-server",
        "//packages/platform-server/testing",
        "//packages/private/testing",
    ],
)

jasmine_node_test(
    name = "test",
    bootstrap = ["//tools/testing:node_es2015"],
    data = [
        ":downleveled_es5_fixture",
        ":istanbul_inheritance_fixture_es2015_instrumented",
        ":istanbul_inheritance_fixture_es5_instrumented",
    ],
    shard_count = 4,
    deps = [
        ":test_lib",
        ":test_node_only_lib",
        "//packages/platform-server",
        "//packages/platform-server/testing",
        "//packages/zone.js/lib:zone_d_ts",
        "@npm//base64-js",
        "@npm//source-map",
    ],
)

karma_web_test_suite(
    name = "test_web",
    runtime_deps = [
        ":downleveled_es5_fixture",
        ":istanbul_inheritance_fixture_es2015_instrumented",
        ":istanbul_inheritance_fixture_es5_instrumented",
    ],
    deps = [
        ":test_lib",
    ],
)
