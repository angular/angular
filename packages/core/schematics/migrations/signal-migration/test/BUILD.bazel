load("@npm//@angular/build-tooling/bazel/integration:index.bzl", "integration_test")
load("//packages/core/schematics/migrations/signal-migration/test/ts-versions:index.bzl", "TS_VERSIONS")
load("//tools:defaults.bzl", "jasmine_node_test", "nodejs_binary", "ts_library")

ts_library(
    name = "test_runners_lib",
    testonly = True,
    srcs = [
        "batch_runner.ts",
        "golden_test_runner.ts",
    ],
    deps = [
        "@npm//@types/diff",
        "@npm//chalk",
        "@npm//diff",
        "@npm//fast-glob",
    ],
)

nodejs_binary(
    name = "golden_test_runner",
    testonly = True,
    data = [":test_runners_lib"],
    entry_point = ":golden_test_runner.ts",
)

nodejs_binary(
    name = "batch_runner",
    testonly = True,
    data = [":test_runners_lib"],
    entry_point = ":batch_runner.ts",
)

integration_test(
    name = "test",
    srcs = [
        "golden.txt",
        "//packages/core/schematics/migrations/signal-migration/test/golden-test:test_files",
    ],
    commands = [
        "$(rootpath //packages/core/schematics/migrations/signal-migration/src:bin) ./golden-test/tsconfig.json --insert-todos",
        "$(rootpath :golden_test_runner) ./golden-test ./golden.txt",
    ],
    data = [
        ":golden_test_runner",
        "//packages/core/schematics/migrations/signal-migration/src:bin",
    ],
    environment = {
        "FORCE_COLOR": "3",
    },
)

[
    integration_test(
        name = "test_ts_%s" % version,
        srcs = [
            "golden.txt",
            "//packages/core/schematics/migrations/signal-migration/test/golden-test:test_files",
            "//packages/core/schematics/migrations/signal-migration/test/ts-versions:hooks.mjs",
            "//packages/core/schematics/migrations/signal-migration/test/ts-versions:loader.mjs",
        ],
        commands = [
            "$(rootpath //packages/core/schematics/migrations/signal-migration/src:bin) --node_options=--import=./ts-versions/loader.js ./golden-test/tsconfig.json --insert-todos ",
            "$(rootpath :golden_test_runner) ./golden-test ./golden.txt",
        ],
        data = [
            ":golden_test_runner",
            "//packages/core/schematics/migrations/signal-migration/src:bin",
            "@npm_ts_versions//%s" % version,
        ],
        environment = {
            "FORCE_COLOR": "3",
            "TS_VERSION_PACKAGE": version,
        },
    )
    for version in TS_VERSIONS
]

integration_test(
    name = "test_best_effort",
    srcs = [
        "golden_best_effort.txt",
        "//packages/core/schematics/migrations/signal-migration/test/golden-test:test_files",
    ],
    commands = [
        "$(rootpath //packages/core/schematics/migrations/signal-migration/src:bin) ./golden-test/tsconfig.json --best-effort-mode --insert-todos",
        "$(rootpath :golden_test_runner) ./golden-test ./golden_best_effort.txt",
    ],
    data = [
        ":golden_test_runner",
        "//packages/core/schematics/migrations/signal-migration/src:bin",
    ],
    environment = {
        "FORCE_COLOR": "3",
    },
)

integration_test(
    name = "test_batch",
    size = "large",
    srcs = [
        "golden.txt",
        "//packages/core/schematics/migrations/signal-migration/test/golden-test:test_files",
    ],
    commands = [
        "$(rootpath :batch_runner) analyze ./golden-test",
        "$(rootpath :batch_runner) merge ./golden-test",
        "$(rootpath :batch_runner) migrate ./golden-test",
        "$(rootpath :golden_test_runner) ./golden-test ./golden.txt",
    ],
    data = [
        ":batch_runner",
        ":golden_test_runner",
        "//packages/core/schematics/migrations/signal-migration/src:batch_test_bin",
        "//packages/core/schematics/migrations/signal-migration/src:bin",
    ],
    environment = {
        "FORCE_COLOR": "3",
    },
    tags = [
        # Only used for manual verification at this point.
        # The test is extremely expensive due to no caching between units.
        "manual",
    ],
    tool_mappings = {
        "//packages/core/schematics/migrations/signal-migration/src:batch_test_bin": "migration",
    },
)

ts_library(
    name = "test_lib",
    testonly = True,
    srcs = ["migration_spec.ts"],
    deps = [
        "//packages/compiler-cli/src/ngtsc/file_system",
        "//packages/compiler-cli/src/ngtsc/file_system/testing",
        "//packages/core/schematics/migrations/signal-migration/src",
        "//packages/core/schematics/utils/tsurge",
    ],
)

jasmine_node_test(
    name = "test_jasmine",
    srcs = [":test_lib"],
)
